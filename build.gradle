// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    versions = [
            gradle: "4.0.1",
            kotlin: "1.3.72",
            androidx_appcompat: "1.1.0",
            androidx_core: "1.3.1",
            junit: "4.13",
            androidx_test_ext: "1.1.1",
            androidx_test_espresso: "3.2.0",
            play_services_maps: "17.0.0",
            places: "2.3.0"

    ]
    libs = [
            gradle: "com.android.tools.build:gradle:${versions.gradle}",
            kotlin: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            androidx_appcompat: "androidx.appcompat:appcompat:${versions.androidx_appcompat}",
            androidx_core: "androidx.core:core-ktx:${versions.androidx_core}",
            androidx_test_espresso: "androidx.test.espresso:espresso-core:${versions.androidx_test_espresso}",
            androidx_test_ext: "androidx.test.ext:junit:${versions.androidx_test_ext}",
            junit: "junit:junit:${versions.junit}",
            play_services_maps: "com.google.android.gms:play-services-maps:${versions.play_services_maps}",
            places: "com.google.android.libraries.places:places:${versions.places}"
    ]
}
project.ext {
    global_compileSdkVersion = 29
    global_buildToolsVersion = "29.0.3"
}
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'digital.wup:android-maven-publish:3.6.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
allprojects {
    group = "com.library.tappyplay.pocketknife"
    version = "1.0.0"
    repositories {
        google()
        jcenter()
    }
    task hello {
        // debug task
        doLast { task ->
            println "I'm $task.project.name (this is the task.project.name)"
        }
    }
}
subprojects {
    apply plugin: "maven-publish"
    apply plugin: 'digital.wup.android-maven-publish'
    hello {
        // debug message
        doLast { println "Message: Checking in subprojects" }
    }
    afterEvaluate { Project project ->
        publishing {// ./gradlew publishToMavenLocal --info
            publications {
                mavenArr(MavenPublication) {
                    artifactId = "$rootProject.name-$project.name"
                    // debug message
                    println "Message: from components.android $rootProject.name-$project.name"
                    from components.android
                }
            }
        }
    }
}




